-- Class Definitions:
-- -- A class definition can be either a class declaration or class refinements
CDList . CDList ::= [CDef] Exp;
CDDecl . CDef ::= CD ;
CDRef . CDef ::= CR ;

-- Class Declaration
CDecl . CD ::= "class" Id "extends" Type "{" [FD] KD [MD] "}" ;

-- Class Refinements
CRef . CR ::= "refines" "class" Id "{" [FD] KR [MD] [MR] "}" ;

-- Fields Declaration
FDecl . FD ::= Type Id ";" ;

-- Constructor Declaration
KDecl . KD ::= Id "(" [Field] ")" "{" "super" "(" [Arg] ")" ";" [Assignment] "}" ;

-- Constructor Refinements 
KRef . KR  ::= "refines" Id "(" [Field] ")" "{" "original" "(" [Arg] ")" ";" [Assignment] "}" ;

Field . Field ::= Type Id ;
FormalArg . FormalArg ::= Type Id ;
Arg . Arg ::= Id ;
Assignment . Assignment ::= "this" "." Id "=" Id ";" ;

-- Method Declaration
MethodDecl . MD ::= Type Id "(" [FormalArg] ")" "{" "return" Term ";" "}" ;

-- Method Refinements 
MethodRef . MR ::= "refines" Id Id "(" [FormalArg] ")" "{" "return" Term ";" "}" ;

TypeObject . Type ::= "Object" ;
TypeId . Type ::= Id ;

TermVar . Term ::= Id ;
TermFieldAccess . Term ::= Term "." Id ; 
TermMethodInvoc . Term ::= Term "." Id "(" [Term] ")" ;
TermExp . Term ::= Exp ;

CastExp . Exp ::= "(" Type ")" Term ;
NewExp . Exp ::= "new" Id "(" [Term] ")" ;

token Id (letter (letter | digit | '_')*) ;

separator CDef "" ;
separator FD "" ;
separator MD "" ;
separator MR "" ;
separator Field "," ;
separator FormalArg "," ;
separator Arg "," ;
separator Assignment "" ;
separator Term "," ;

