-- Class Declaration

CList . CL ::= [CD] Exp;

CDecl . CD ::= "class" Id "extends" Type "{" [FD] KD [MD] "}" ;

-- Fields Declaration
FDecl . FD ::= Type Id ";" ;

-- Constructor Declaration
KDecl . KD ::= Id "(" [Field] ")" "{" "super" "(" [Arg] ")" ";" [Assignment] "}" ;

Field . Field ::= Type Id ;
FormalArg . FormalArg ::= Type Id ;
Arg . Arg ::= Id ;
Assignment . Assignment ::= "this" "." Id "=" Id ";" ;

-- Method Declaration
MethodDecl . MD ::= Type Id "(" [FormalArg] ")" "{" "return" Term ";" "}" ;

TermVar . Term ::= Id ;
TermFieldAccess . Term ::= Term "." Id ; 
TermMethodInvoc . Term ::= Term "." Id "(" [Term] ")" ;
TermExp . Term ::= Exp ;

CastExp . Exp ::= "(" Type")" Term ;
NewExp . Exp ::= "new" Id "(" [Term] ")" ;

Identifier . Id ::= String ;

TypeObject . Type ::= "Object" ;
TypeId . Type ::= Id ;

separator CD "" ;
separator FD "" ;
separator MD "" ;
separator Field "," ;
separator FormalArg "," ;
separator Arg "," ;
separator Assignment "" ;
separator Term "," ;

