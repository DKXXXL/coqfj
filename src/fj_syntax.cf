-- Class Declaration

CProgram . Program ::= [ClassDecl] Exp;

CDecl . ClassDecl ::= "class" Id "extends" Type "{" [FieldDecl] Constructor [MethodDecl] "}" ;

-- Fields Declaration
FDecl . FieldDecl ::= Type Id ";" ;

-- Constructor Declaration
KDecl . Constructor ::= Id "(" [Field] ")" "{" "super" "(" [Arg] ")" ";" [Assignment] "}" ;

Field . Field ::= Type Id ;
FormalArg . FormalArg ::= Type Id ;
Arg . Arg ::= Id ;
Assignment . Assignment ::= "this" "." Id "=" Id ";" ;

-- Method Declaration
MethodDecl . MethodDecl ::= Type Id "(" [FormalArg] ")" "{" "return" Term ";" "}" ;

TermVar . Term ::= Id ;
TermFieldAccess . Term ::= Term "." Id ; 
TermMethodInvoc . Term ::= Term "." Id "(" [Term] ")" ;
TermExp . Term ::= Exp ;

CastExp . Exp ::= "(" Type")" Term ;
NewExp . Exp ::= "new" Id "(" [Term] ")" ;

token Id (letter (letter | digit | '_')*) ;

TypeObject . Type ::= "Object" ;
TypeId . Type ::= Id ;

separator ClassDecl "" ;
separator FieldDecl "" ;
separator MethodDecl "" ;
separator Field "," ;
separator FormalArg "," ;
separator Arg "," ;
separator Assignment "" ;
separator Term "," ;

