-- Class Declaration
-- CList . CL ::= [CD] ;
CDecl . CD ::= "class" Id "extends" Id "{" [FD] KD [MD] "}" ;

-- Fields Declaration
FDecl . FD ::= Id Id ";" ;

-- Constructor Declaration
KDecl . KD ::= Id "(" [Field] ")" "{" "super" "(" [Arg] ")" ";" [Assignment] "}" ;

Field . Field ::= Id Id ;
FormalArg . FormalArg ::= Id Id ;
Arg . Arg ::= Id ;
Assignment . Assignment ::= "this" "." Id "=" Id ";" ;

-- Method Declaration
MethodDecl . MD ::= Id Id "(" [FormalArg] ")" "{" "return" Term ";" "}" ;

TermVar . Term ::= Id ;
TermFieldAccess . Term ::= Term "." Id ; 
TermMethodInvoc . Term ::= Term "." Id "(" [Term] ")" ;
TermObjectCreation . Term ::= "new" Id "(" [Term] ")" ;
TermCast . Term ::= "(" Id ")" Term ;

Identifier . Id ::= String ;

separator CD "" ;
separator FD "" ;
separator MD "" ;
separator Field "," ;
separator FormalArg "," ;
separator Arg "," ;
separator Assignment "" ;
separator Term "," ;

