-- Class Declaration
CDecl . CD ::= "class" Id "extends" Id "{" FD KD MDL "}" ;

-- Fields Declaration
NoneFields . FD ::= ;
OneOrMoreFieldsDecl . FD ::= FD Id Id ";" ;

-- Constructor Declaration
KDecl . KD ::= Id "(" FL ")" "{" "super" "(" IDL ")" ";" THISATTRL ";" "}" ;

EmptyFieldsList . FL ::= EMPTYL ; 
OneOrMoreFields . FL ::= MULTFL ;
OneField . MULTFL ::= Id Id ;
MoreThanOneField . MULTFL ::= MULTFL "," Id Id ;

EmptyVarList . IDL ::= EMPTYL ;
OneOrMoreVar . IDL ::= MULTID ;
OneVar . MULTID ::= Id ;
MoreThanOneVar . MULTID ::= MULTID "," Id ;

EmptyListOfAttr . THISATTRL ::= ;
ThisAttrList . THISATTRL ::= THISATTRL "this." Id "=" Id ";" ;

EmptyMethodosList . MDL ::= EMPTYL ;
OneOrMoreMethodDeclaration . MDL ::= MULTMETHODS ;
OneMethod . MULTMETHODS ::= MD ;
MultMethods . MULTMETHODS ::= MULTMETHODS MD ;

-- Method Declaration
MethodDecl . MD ::= Id Id "(" FL ")" "{" "return" Term ";" "}" ;

TermVar . Term ::= Id ;
TermFieldAccess . Term ::= Term "." Id ; 
TermMethodInvoc . Term ::= Term "." Id "(" TL ")" ;
TermObjectCreation . Term ::= "new" Id "(" TL ")" ;
TermCast . Term ::= "(" Id ")" Term ;

EmptyTermsList . TL ::= EMPTYL ;
OneOrMoreTerms . TL ::= MultipleTerms;
OneTerm . MultipleTerms ::= Term ;
MoreThanOneTerm . MultipleTerms ::= MultipleTerms Term ;

Identifier . Id ::= String ;

EmptyList . EMPTYL ::= ;
